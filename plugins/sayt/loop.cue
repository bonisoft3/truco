// The loop, published as CUE: sayt's development-lifecycle contract for one
// app — the third first-class pronto concept beside the virtual cluster
// (mecha:cluster) and the virtual terminal (omnishell:terminal). Programs
// target a cluster, users touch a terminal, developers turn the loop.
// Override by unification; pin or fork this package to version the loop.
//
// Doctrine carried as data and conventions:
// - Verbs ride their builtins against the files this contract emits:
//   build/test read tasks.json (ide layer), launch drives compose.yaml's
//   `launch` service gate, lint runs the rulemap below.
// - Commands are argv-shaped: the task runner splits on spaces, no shell.
// - verify is agent-driven by default: the storybook renders the evidence,
//   the driving agent judges it; the API-key vision harness is the
//   unattended-CI upgrade.
package loop

import (
	"list"
	"strings"

	saycfg "bonisoft.org/plugins/sayt:say"
)

#Loop: L={
	meta: {app: string}

	// The emitted .say.yaml must load under sayt's own config schema
	// (package say, config.cue). Validation is a hidden unification rather
	// than a type on sayYaml itself: typing the exported field would resolve
	// the schema's defaults into the emitted file, and .say.yaml carries
	// only the app's deltas — sayt applies the schema at load time.
	_sayValid: saycfg.say & L.surface.sayYaml.say

	// Declared checks projected into rulemap shape, bucketed by the layer each
	// one needs. Keyed by every verb so a bucket is an empty struct rather than
	// absent, which is what lets sayYaml test its length.
	// Flat, and a LIST on purpose: `len()` over the struct the comprehension
	// below builds evaluates as incomplete and the setup guard silently never
	// fires — the emitted .say.yaml simply loses the rule with no error. A list
	// comprehension over the same source is concrete.
	_setupCmds: [for _, c in L.surface.verbs if c.verb == "setup" for d in c.cmds {do: d}]
	_launchCmds: [for _, c in L.surface.verbs if c.verb == "launch" for d in c.cmds {do: d}]

	_rulesFor: {
		for verb in ["lint", "test", "integrate"] {
			(verb): {
				for name, c in L.surface.checks if c.verb == verb {
					(name): {
						priority?: int
						if c.priority != _|_ {priority: c.priority}
						cmds: [for d in c.cmds {do: d}]
					}
				}
			}
		}
	}

	surface: {
		sources: [string]: string
		sources: {}
		// argv-shaped commands (see doctrine above).
		buildCmd: string
		testCmd:  string

		// Validator surfaces for the lint rulemap.
		pipelineFiles: [...string] // docker/<app>-<name>.yaml, all kinds; empty = no rule

		// What the virtual cluster and terminal declare about their own
		// surfaces; emit.cue merges both runtimes' sets in here.
		//
		// Two fields because a sayt layer is a pair whose halves differ in
		// kind: one acts, one judges. The verb is what the work needs, so a
		// battery that measures a rendered page cannot land at lint however
		// cheap it looks.
		// A release verb names the platform its rule answers for, builds its
		// artifact in `cmds`, and makes it live in `publish`: the ceremony,
		// release.nu's version and tag, runs between the two, and `publish` is
		// skipped under --snapshot, which is what "build only" means.
		verbs: [Name=string]: {verb: "setup" | "generate" | "build" | "launch" | "release", cmds: [...string], note: string, platform?: string, publish: *[] | [...string]}
		verbs: {}
		checks: [Name=string]: {verb: "lint" | "test" | "integrate", cmds: [...string], note: string, priority?: int}
		checks: {}

		sayYaml: {
			say: {
				...
				if len([for _, c in L.surface.verbs if c.verb == "generate" {c}]) > 0 {
					generate: rulemap: {
						...
						for name, c in L.surface.verbs if c.verb == "generate" {
							(name): {priority: 0, cmds: [for d in c.cmds {do: d}]}
						}
					}
				}
				if len([for _, c in L.surface.verbs if c.verb == "release" {c}]) > 0 {
					release: rulemap: {
						...
						for name, c in L.surface.verbs if c.verb == "release" {
							(name): {
								if c.platform != _|_ {platform: c.platform}
								// A rule of several commands gets the verb's flags as
								// environment, not on each command's line, and a flag
								// is only a flag to nushell when it is spelled in the
								// call, so the ceremony spells --snapshot itself where
								// the verb was given it. goreleaser's git-state
								// validation refuses the prefixed monorepo tag, and
								// --clean wipes its dist between runs.
								cmds: list.Concat([
									[for d in c.cmds {do: d}],
									[{do: "if ($env.SAY_RELEASE_ARGS_SNAPSHOT? | is-empty) { release --skip=validate --clean } else { release --snapshot --skip=validate --clean }", use: "./release.nu"}],
									[for p in c.publish {do: "if ($env.SAY_RELEASE_ARGS_SNAPSHOT? | is-empty) { \(p) }"}],
								])
							}
						}
					}
				}
				lint: rulemap: {
					L._rulesFor.lint
					...
					"cue": cmds: [{do: "mise exec -- cue vet ./..."}]
					// Guarded like handlers and screens below: with no pipeline
					// files the command lints its own empty argument list, which
					// passes without reading anything.
					if len(L.surface.pipelineFiles) > 0 {
						"rpk": cmds: [{
							do: "mise exec -- redpanda-connect lint --skip-env-var-check " +
								strings.Join(L.surface.pipelineFiles, " ")
						}]
					}
				}
				// A runtime that declares nothing at these layers emits no key at
				// all, leaving the app's builtin test/integrate untouched.
				//
				// Where it DOES declare, config.cue's builtin rule carries
				// `stop: true`, so untouched it runs first and the declared rules
				// never do. `stop: false` alone fails the default disjunct and
				// resolves the builtin with NO cmds — so every cmd the builtin
				// should keep must be re-declared beside it (setup below is the
				// same pattern). test keeps its builtin: `./test.nu` runs
				// tasks.json's `cue vet -c ./...`, the concreteness gate the
				// declared batteries assume. integrate re-declares nothing on
				// purpose: its builtin would `down -v` and `up integrate` on its
				// own, and the runtime a declared check brought up in front of
				// itself would go down with it, so the declared checks are the
				// whole verb.
				if len(L._rulesFor.test) > 0 {
					test: rulemap: {
						L._rulesFor.test
						builtin: {stop: false, cmds: [{do: "test", use: "./test.nu"}]}
					}
				}
				if len(L._rulesFor.integrate) > 0 {
					integrate: rulemap: {L._rulesFor.integrate, builtin: {stop: false}}
				}

				// setup is the one layer whose declarations must APPEND to the
				// builtin rather than sit beside it. config.cue gives the builtin
				// rule `stop: true`, so a sibling rule is emitted, sorted after it,
				// and never runs; and `builtin: {stop: false}` fails the default
				// disjunct, which silently resolves builtin with no cmds at all —
				// setup would stop installing tools and say nothing. Re-declaring
				// the builtin's own cmd first is what keeps `mise install` in the
				// chain (services/esocial-rpa is the hand-written worked example).
				if len(L._setupCmds) > 0 {
					setup: rulemap: builtin: cmds: list.Concat([
						[{do: "setup", use: "./setup.nu"}],
						L._setupCmds,
					])
				}

				// A launch declaration is a precondition of the stack — compose is
				// up by the time the builtin returns — so these prepend where
				// setup's append. A rule beside the builtin would not run, for
				// the reason setup's comment gives.
				if len(L._launchCmds) > 0 {
					launch: rulemap: builtin: cmds: list.Concat([
						L._launchCmds,
						[{do: "launch", use: "./launch.nu"}],
					])
				}
			}
		}

		tasksJson: {
			version: "2.0.0"
			tasks: [
				{
					label:   "build"
					type:    "shell"
					command: L.surface.buildCmd
					group: {kind: "build", isDefault: true}
				},
				{
					label:   "test"
					type:    "shell"
					command: L.surface.testCmd
					group: {kind: "test", isDefault: true}
				},
			]
		}
	}
}
